apply plugin: 'com.android.library'
apply plugin: 'digital.wup.android-maven-publish'
apply plugin: 'kotlin-android'

android {

    final def globalConfiguration = rootProject.extensions.getByName("ext")

    compileSdkVersion globalConfiguration["androidCompileSdkVersion"]
    buildToolsVersion globalConfiguration["androidBuildToolsVersion"]

    defaultConfig {

        minSdkVersion globalConfiguration["androidMinSdkVersion"]
        targetSdkVersion globalConfiguration["androidTargetSdkVersion"]

        versionCode globalConfiguration["androidVersionCode"]
        versionName globalConfiguration["androidVersionName"]
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        checkAllWarnings true
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFile getDefaultProguardFile('proguard-android.txt')
            proguardFile 'proguard-keep-as-api.pro'
        }
    }

    flavorDimensions 'default'

    productFlavors {
        exposed {
            proguardFile 'proguard-configurable-keep.pro'
        }

        production {
        }
    }

    configurations {
        javadocDeps
    }
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.javadocDeps
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.sourceFiles
}

publishing {
    repositories {
        maven {
            url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
        }
    }

    publications {
        mavenAar(MavenPublication) {
            from components.android
            artifact androidSourcesJar
            artifact androidJavadocsJar
            groupId = 'com.github.doctoror.particlesdrawable'
            artifactId = 'library'
            version = rootProject.extensions.getByName("ext")["androidVersionName"]

            pom {
                name = 'Particles Drawable'
                description = 'Android Particles Drawable and View'
                url = 'https://github.com/Doctoror/ParticlesDrawable'
                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'Doctoror'
                        name = 'Yaroslav Mytkalyk'
                    }
                }
                scm {
                    connection = 'scm:git@github.com:Doctoror/ParticlesDrawable.git'
                    developerConnection = 'scm:git@github.com:Doctoror/ParticlesDrawable.git'
                    url = 'https://github.com/Doctoror/ParticlesDrawable'
                }
            }
        }
    }
}

dependencies {
    final def d = rootProject.ext.libraryDependencies
    final def td = rootProject.ext.libraryTestDependencies

    testImplementation td.androidxTestCore
    testImplementation td.junit4
    testImplementation td.junitJupiter
    testImplementation td.junitVintage
    testImplementation td.kotlin
    testImplementation td.mockitoCore
    testImplementation td.mockitoKotlin
    testImplementation td.robolectric

    javadocDeps d.annotations

    implementation d.annotations
}
