apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {

    def globalConfiguration = rootProject.extensions.getByName("ext")

    compileSdkVersion globalConfiguration["androidCompileSdkVersion"]
    buildToolsVersion globalConfiguration["androidBuildToolsVersion"]

    defaultConfig {
        applicationId "com.doctoror.particleswallpaper"

        minSdkVersion globalConfiguration["androidMinSdkVersion"]
        targetSdkVersion globalConfiguration["androidTargetSdkVersion"]

        versionCode 1
        versionName "1.0.0"

        resConfigs "en"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    signingConfigs {

        release {
            storeFile file("../keystore/doctoror.keystore")
        }
    }

    buildTypes {
        release {
            debuggable false
            signingConfig signingConfigs.release

            if (project.hasProperty('keyAlias')) {
                android.signingConfigs.release.keyAlias = keyAlias
            }

            if (project.hasProperty('storePassword')) {
                android.signingConfigs.release.storePassword = storePassword
            }

            if (project.hasProperty('keyPassword')) {
                android.signingConfigs.release.keyPassword = keyPassword
            }

            minifyEnabled true
            shrinkResources true

            proguardFile getDefaultProguardFile('proguard-android.txt')
            proguardFile 'proguard-lifecycle.pro'
            proguardFile 'proguard-picasso.pro'
            proguardFile 'proguard-rx-java.pro'
            proguardFile 'proguard-rules.pro'
        }
    }

    lintOptions {
        checkAllWarnings true
    }

    task copyTestClasses(type: Copy) {
        from "build/tmp/kotlin-classes/debugUnitTest"
        into "build/intermediates/classes/debug"
    }

    task copySdkClasses(type: Copy) {
        from "build/tmp/kotlin-classes/debug"
        into "build/intermediates/classes/debug"
    }

//    afterEvaluate {
//        compileDebugUnitTestSources.dependsOn copyTestClasses
//        compileReleaseUnitTestSources.dependsOn copyTestClasses
//        compileDebugAndroidTestSources.dependsOn copySdkClasses
//    }
}

dependencies {
    def d = rootProject.ext.wallpaperappDependencies
    def td = rootProject.ext.wallpaperappTestDependencies

    androidTestCompile td.testRunner
    androidTestCompile td.testRules

    androidTestCompile td.kotlin
    androidTestCompile td.kotlinTest
    androidTestCompile td.kotlinReflect

    androidTestCompile td.mockitoAndroid

    testCompile td.kotlin
    testCompile td.kotlinTest

    testCompile td.junit

    compile project (':library')

    compile d.kotlin
    compile d.rxJava

    compile d.colorPicker
    compile d.picasso

    compile d.gendalf
    annotationProcessor d.gendalfCompiler
    kapt d.gendalfCompiler

    compile d.dagger
    annotationProcessor d.daggerCompiler
    kapt d.daggerCompiler

    compile d.lifecycle
    annotationProcessor d.lifecycleCompiler
    kapt d.lifecycleCompiler
}

repositories {
    mavenCentral()
}
